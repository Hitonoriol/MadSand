gradle.startParameter.showStacktrace = org.gradle.api.logging.configuration.ShowStacktrace.ALWAYS

/* App metadata */
ext {
	appName = "MadSand"
	versionCode = '0.49.19'
	author = 'Hitonoriol'
}

ext {
	rootDir = file(".").absolutePath
	assetsDir = file("${rootDir}/core/assets")
}

/* System properties */
ext {
	javaVersion = 18
	javaHome = System.getProperty('java.home')
	javaBin = "${javaHome}/bin"
	osName = System.getProperty('os.name').toLowerCase(Locale.ROOT).split(' ')[0]
	pathSep = File.pathSeparatorChar
}

/* Dependency versions */
ext {
	gdxVersion = '1.11.0'
	aiVersion = '1.6.0'
	jacksonVersion = '2.13.4'
}

/* jlink configuration */
ext {
	jlinkModuleDir = "${javaHome}/jmods"
	jlinkModuleList = "java.base,java.desktop,jdk.unsupported,java.management,jdk.crypto.ec"
}

allprojects {
	apply plugin: "eclipse"
	version = "v${versionCode}a"

	plugins.withType(JavaPlugin) {
		compileJava {
			options.release = project.javaVersion
		}
		dependencies {
			implementation project(":commons")
		}
	}
	
	repositories {
		mavenLocal()
		mavenCentral()
	}
	
	buildscript {
		repositories {
			mavenCentral()
		}

		dependencies {
			classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
		}
	}
}

project(":desktop") {
	apply plugin: "java-library"

	dependencies {
		implementation project(":core")
		api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
		api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
		api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
	}
}

project(":core") {
	apply plugin: "java-library"

	dependencies {
		api "com.badlogicgames.gdx:gdx:$gdxVersion"
		api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
		api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
		
		implementation 'org.apache.commons:commons-lang3:3.9'
		implementation 'org.apache.commons:commons-text:1.9'
		implementation 'org.apache.commons:commons-math3:3.6.1'
		implementation 'org.apache.commons:commons-collections4:4.4'
		
		implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
		implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
		implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
		
		implementation 'me.xdrop:fuzzywuzzy:1.3.1'
		
		implementation files('../lib/luaj-jse-3.0.2.jar')
		implementation files('../lib/jrand-0.2.7-alpha.jar')
	}
}